# 명세

'찰떡' 프로그램은 다음 요소로 구성된 문자열입니다.
- 어휘 정의문
- 구문 정의문
- 본문
- 주석

'찰떡' 인터프리터는 본문을 해석하여 값을 반환하며, 이에 필요한 어휘 분석기와 구문 분석기를 포함하고 있습니다. 어휘 분석기와 구문 분석기는 각각 어휘 정의문과 구문 정의문을 바탕으로 구성됩니다.

## 본문
본문은 구문 정의문에 속하지 않는 모든 표현식을 말합니다.

본문은 고유한 문맥에서 실행되며, 마지막으로 실행된 표현식의 결과가 출력됩니다. 고유한 문맥에서 실행된다는 것은 구문 정의문 내부 등 다른 문맥에 영향을 주지 않는다는 것을 말합니다.

## 표현식
표현식은 마침표로 끝나는 문장으로 이루어진 문자열로, 프로그램 실행의 기본 단위입니다.
문장은 공백으로 구분된 여러 어절로 이루어집니다. 어절은 다음과 같이 중심 단어의 뒤에 여러 조사, 어미, 접미사가 선택적으로 결합한 형태입니다.

* `삼천이백` <- `삼천이백` (중심 단어)
* `다섯제곱의` <- `다섯` (중심 단어) + `-제곱` (접미사) + `의` (조사)
* `더하자` <- `더하다` (중심 단어) + `-자` (어미)

'찰떡'에서 중심 단어는 명사, 동사, 형용사, 관형사, 부사의 다섯 가지 품사를 지닌 단어가 올 수 있으며, 명사는 뒤에 접미사 및 조사와, 동사와 형용사는 뒤에 어미와 결합할 수 있습니다.

### 표현식의 해석
표현식의 해석에는 어휘 분석기와 구문 분석기가 필요합니다. 표현식을 해석하면 그 결과로 값을 얻습니다.

어휘 분석기는 표현식을 단어의 나열로 분석합니다.
예를 들어, 표현식 `1과 2를 더한 값.`은 다음의 단어들로 분석됩니다.
- `1[한자어수사]`
- `과[조사]`
- `2[한자어 수사]`
- `를[조사]`
- `더하다[동사]`
- `-ㄴ[어미]`
- `값[명사]`

구문 분석기는 낱말의 나열을 구 단위로 구조화하여 하나의 구로 묶습니다.
예를 들어, 표현식 `1과 2를 더한 값.`은 다음의 구로 분석됩니다.
- `1[한자어수사]` -> `{}[명사]`
- `2[한자어수사]` -> `{}[명사]`
- `{}[명사] 과[조사] {}[명사] 를[조사] 더하다[동사]` -> `{}[동사]`
- `{}[동사] -ㄴ[어미] 값[명사]` -> `{}[명사]`

위 표현식은 다음과 같은 순서로 실행되어 정수 3을 결과값으로 내놓습니다.
- 구 `1[한자어수사]`이 정수 1을 내놓습니다.
- 구 `2[한자어수사]`가 정수 2을 내놓습니다.
- 구 `{}[명사] 과[조사] {}[명사] 를[조사] 더하다[동사]`가 정수 3을 내놓습니다.
- `{}[동사] -ㄴ[어미] 값[명사]`가 정수 3을 내놓습니다.

### 자료형
'찰떡'에서 값은 '수', '정수', '참거짓', '나눔'이라는 네 가지 자료형을 지닙니다. '수'와 '정수'는 아래에서 설명할 수사로, '참거짓'은 `참`, `거짓`이라는 단어로 생성할 수 있습니다.

'나눔'은 `어느 수로 어느 수를 나누다`와 같은 구문으로 나눗셈을 수행한 결과로 얻는 자료형입니다. `...을 나눈 값`, `...을 나눈 나머지`, `...을 나눈 몫` 등과 같은 꼴로 각각 실수 나눗셈 결과, 정수 나눗셈의 몫, 정수 나눗셈의 나머지를 얻을 수 있습니다.

### 수사
'찰떡'은 다음과 같은 형식의 수사를 인식합니다.

* 쉼표로 구분된 인도-아라비아 숫자
  + 예: `0.12`, `+12,345,678.900`
* 지수 표기법
  + 예: `−1.602e−19`
* 만의 자리에서 띄어쓴 한자어 수사
  + 예: `일억 이천삼백사십오만 육천칠백팔십구`
* 십의 자리마다 인도-아라비아 숫자를 적은 한자어 수사
  + 예: `1억 2천3백4십5만 6천7백8십9`
* 만의 자리마다 인도-아라비아 숫자를 적은 한자어 수사
  + 예: `1억 2345만 6789`
* 고유어 수사
  + 예: `하나`, `둘`
* 고유어 수관형사
  + 예: `한`, `두`

이 가운데 고유어 수사는 특수한 경우에만 사용되며, [인수 참조 어휘](./param-ref.md) 문서에서 더 자세한 내용을 확인할 수 있습니다.

### 변수
'찰떡'에서 변수의 이름은 `'결과'`처럼 따옴표(`'...'`)로 감싼, 공백을 포함하지 않는 문자열로 표기합니다. 이 규칙은 변수를 선언할 때와 사용할 때 모두 적용됩니다.

## 주석
주석은 다음 형식과 같이 짝이 맞는 소괄호로 묶인 문자열입니다.

    (주석 내용)

주석 안에 다시 소괄호가 있을 수 있습니다. 그러한 경우 주석 안의 소괄호 역시 짝을 맞춰야 합니다.

예:
```
주석 바깥 ( 주석 안 ( 주석 안 ) 주석 안 ) 주석 바깥
```

주석 안에는 줄바꿈이 들어갈 수 있습니다.

예:
```
(
    주석
)
```

모든 주석은 프로그램 실행 시 존재하지 않는 부분으로 인식되며, 어휘 및 구문 분석 등에 영향을 주지 않습니다.

## 어휘 정의문
어휘 정의문은 어휘 분석기에 어휘를 등록하기 위한 구문입니다.
어휘 분석기는 표현식을 구성하는 문자열을 어휘의 나열로 분석합니다.
어휘를 어휘 분석기에 등록하지 않으면 표현식에서 해당 어휘를 포함할 때 분석에 실패합니다.

어휘 정의문은 다음과 같은 형식의 문자열입니다. 어휘 정의문은 줄이 바뀌면 끝납니다.

    기본형 [품사] 문법 정보

기본형은 정의하려는 어휘의 기본이 되는 형태로, 사전의 표제어를 올릴 때 사용되는 것과 같습니다. 예를 들어, 동사 '나누다, 나눠, 나누니'의 기본형은 '나누다'입니다.

품사는 다음 8가지를 사용할 수 있습니다.
- `[명사]`
- `[동사]`
- `[형용사]`
- `[관형사]`
- `[부사]`
- `[조사]`
- `[어미]`
- `[접미사]`

동사와 형용사, 어미는 문법 정보가 필요할 때가 있습니다. 그밖의 품사는 문법 정보를 적지 않습니다.

### 동사와 형용사
동사와 형용사는 활용형을 다음 형식에 맞춰 문법 정보에 적습니다.

> 기본형 [품사] -아/어꼴, -으니꼴

예:
- `같다 [형용사] 같아, 같으니`
- `크다 [형용사] 커, 크니`
- `다르다 [형용사] 달라, 다르니`

활용형이 여러 가지로 나타날 수 있는 경우 빗금표(`/`)로 구분합니다.

예:
- `제곱하다 [동사] 제곱하여/제곱해, 제곱하니`
- `두다 [동사] 두어/둬, 두니`

규칙 용언의 경우 활용형을 생략할 수 있습니다.

예:
- `많다 [형용사]`
- `적다 [형용사]`

### 조사
조사는 선행하는 단어의 받침에 따라 그 형태가 달라질 수 있습니다.
그러한 경우 다음 형식을 따릅니다.

    받침이 있을 때/받침이 없을 때 [조사]

예:
- `은/는 [조사]`
- `이/가 [조사]`
- `을/를 [조사]`

조사의 형태가 선행하는 단어의 받침과 무관하다면 그 형태만 적습니다.

예:
- `에 [조사]`
- `의 [조사]`

### 어미
어미는 기본형의 앞에 붙임표(`-`)를 덧붙이며, 선행하는 용언이 받침으로 끝나는 경우의 형태를 적습니다.

예:
- `-거나 [어미]` (많거나, 적거나, ...)
- `-어서 [어미]` (많아서, 적어서, ...)
- `-으면 [어미]` (많으면, 적으면, ...)

받침이 있을 때의 형태만으로 받침이 없을 때의 형태를 알기 어려운 경우 다음 형식에 따라 둘 모두 적습니다.

> -받침이 있을 때/받침이 없을 때 [어미]

예:
- `-는다/ㄴ다 [어미]`

소괄호를 사용하면 주석으로 해석되니 주의가 필요합니다.
예를 들어, 다음 세 어휘 정의문은 동일합니다.
- `-나 [어미]`
- `-(으)나 [어미]`
- `-(임의의 주석)나 [어미]`

어미의 앞에 올 수 있는 단어의 종류에 제한이 있는 경우 다음 형식에 따라 문법 정보에 적습니다.
제한이 없는 경우 문법 정보를 생략합니다.

> 기본형 [어미] 종류[, 종류, ...] 뒤에

제한을 걸 수 있는 종류는 다음과 같습니다.
- 동사
- 형용사
- 이다
- 아니다
- 있다
- 없다

예:
- `-는 [어미] 있다, 없다, 동사 뒤에`

## 구문 정의문

구문 정의문은 구문 분석기에 구문을 등록하기 위한 구문입니다.
구문 분석기는 표현식을 어휘의 나열로 분석한 이후에 이를 구 단위로 구조화합니다. 구는 실행되면 값으로 바뀌며, 모든 표현식은 하나의 구로 구성되어야 합니다.
구문을 구문 분석기에 등록하지 않으면 표현식에서 해당 구문을 포함할 때 분석에 실패합니다.

구문 정의문은 다음 형식과 같이 여러 구문과 내부 표현식으로 구성된 문자열로, 나열한 구문 중 하나가 인식되면 내부 표현식을 실행하여 값을 반환합니다. 구문 정의문은 줄바꿈을 두 번 하여 끝납니다. 구문과 구문, 구문과 표현식 사이에는 임의의 공백과 임의 개수의 줄바꿈이 들어갈 수 있습니다. 표현식과 표현식 사이에는 임의의 공백과 최대 한 개의 줄바꿈이 들어갈 수 있습니다.

```
구문 1:
구문 2:
...
구문 n:

내부 표현식 1.
내부 표현식 2.
...
내부 표현식 m.

```

각 구문은 다음과 같은 인수 정의 어휘를 포함한 문자열입니다. 인수 정의 어휘는 이미 분석된 다른 구를 인수로서 인식하여 내부 표현식에서 사용할 수 있게 합니다.
- `무엇`: 임의의 명사구를 인식합니다.
- `어찌하다`: 임의의 동사구를 인식합니다.
- `어떠하다`: 임의의 형용사구를 인식합니다.
- `얼마`: 값의 자료형이 `수 1개`인 명사구를 인식합니다.
- 그 밖의 인수 정의 어휘는 [인수 정의 어휘](./param-def.md) 문서를 참조해주세요.

이 구문을 사용한 표현식이 실행되면 구문 정의문의 내부 표현식을 차례로 해석한 뒤 마지막 표현식의 결과값을 내놓습니다.
구문 정의문의 내부 표현식은 고유한 문맥 안에서 실행됩니다. 즉, 다른 구문 정의문 내부나 본문 등 다른 문맥에 영향을 주지 않습니다.

구문 정의문 내부 표현식에서는 다음과 같은 인수 참조 어휘를 사용하여 인수를 사용할 수 있습니다. 현재는 인수를 최대 2개까지 지원합니다.
- `해당 수`: (인수가 1개이며 그 자료형이 `수 1개`일 때) 해당 인수.
- `앞의 것`: (인수가 2개일 때) 첫번째 인수.
- `뒤의 것`: (인수가 2개일 때) 두번째 인수.
- 그 밖의 인수 참조 어휘는 [인수 참조 어휘](./param-ref.md) 문서를 참조해주세요.

예를 들어, 다음과 같은 찰떡 프로그램의 경우 `얼마`는 `3과 5의 합`이라는 구를 인수로 인식하며 `해당 수`에서 이 인수를 참조합니다.
```
역수 [명사]
얼마의 역수:
  1을 해당 수로 나눈 값.  

3과 5의 합의 역수. (=> 0.125)
```

## 구문 분석
표현식을 구 단위로 분석할 때는 다음의 규칙에 따릅니다.

- 기본적으로 앞쪽의 단어부터 결합
- 최대한 많은 단어와 일치하는 구문을 선택
- 최대 1개의 인수 생략 가능

### 단어 결합 규칙
한국어와 같은 자연어는 같은 문장이 여러가지 의미로 읽힐 수 있는 중의성이 있습니다. 특히 다음과 같이 단어를 결합하는 순서에 따라 다른 의미를 지니는 구조적 중의성을 생각할 수 있습니다.

![머리가 빨간 생선을 먹는 고양이](https://pbs.twimg.com/media/BnqSR2IIEAAIPW4?format=png&name=medium)
(그림 출처: [트위터 @kjoonlee](https://twitter.com/kjoonlee/status/466846348541558784))

'찰떡'에서는 기본적으로 앞쪽의 단어부터 먼저 결합하는 좌결합 방식을 채택하고 있습니다. 쉼표(`,`)를 사용하면 쉼표 앞뒤 어휘의 결합을 문장이 끝나거나 다음 쉼표를 만나기 전까지 최대한 후순위로 미룹니다.

* `2분의 5분의 1.` → `0.4`
  + 가장 앞에 있는 `2분의 5`가 먼저 결합됩니다.
* `2분의, 5분의 1.` → `0.1`
  + 뒤에 있는 `5분의 1`이 먼저 결합됩니다.

특히 결합 순서로 인해 유효한 문장이 만들어지지 않는 경우 다음과 같이 쉼표를 사용해 원하는 결합 순서를 강제해야 합니다.
* `1과 2의 곱과 3과 4의 곱의 합.`
  + `1과 2의 곱과 3과 4`가 먼저 결합합니다. `합`은 두 개 이상의 인수를 요구하기 때문에 구문 분석 오류가 발생합니다.
* `1과 2의 곱과, 3과 4의 곱의, 합.`
  + `1과 2의 곱` 및 `3과 4의 곱`이 각자 먼저 결합합니다. 원하는 결과 `14`를 얻습니다.

### 인수 생략
구문을 사용할 때 인수를 생략하면 이전 구문의 결과값을 가져와 해당 인수로 전달할 수 있습니다. 인수는 한 개까지만 생략할 수 있습니다. 

인수 생략 시 전달할 값을 지정하려면 어미 `-(아/어)`를 사용합니다.

예를 들어 다음 두 문장은 같은 의미입니다.
* `1과 2를 더해 3에서 뺀 값`
* `3에서 1과 2를 더한 것을 뺀 값`

### 복수 구문 해소
같은 꼴의 구문 정의문을 인수의 자료형을 달리하여 여러 번 정의할 수 있습니다.
해당 구가 실행될 때에 인수의 자료형을 보고 동적으로 구문을 선택하여 실행합니다. 
'정수'와 '나눔'은 필요 시 인수로 '수'를 요구하는 구문도 선택할 수 있습니다.
여러 구문이 모두 선택 가능한 경우 가장 구체적인 자료형을 요구하는 구문을 선택합니다.

예를 들어, 다음 '찰떡' 프로그램에서 `2.5의 반수`는 `어느 수의 반수` 구문을, `2의 반수`는 `어느 정수의 반수` 구문을 선택합니다.
```
반수 [명사]

어느 수의 반수: 해당 수와 -1을 곱한 값.  ( 가 )

어느 정수의 반수: 해당 정수와 -1을 곱한 값.  ( 나 )

2.5의 반수와  ( 가 선택 )
2의 반수의    ( 나 선택 )
곱.
```

## 기본 제공 기능
'찰떡'에서 기본적으로 제공하는 어휘와 구문들이 있습니다.
[기본 제공 기능 일람](./builtin/general.md) 문서를 참조해주세요.

